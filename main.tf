terraform {
  required_version = ">= 0.13.1" # see https://releases.hashicorp.com/terraform/
}

locals {
  memory_store_name         = format("memcache-%s-%s", var.name, var.name_suffix)
  memory_store_display_name = "Memcache generated by Terraform ${var.name_suffix}"
  region                    = data.google_client_config.google_client.region
  zones                     = formatlist("${local.region}-%s", var.zone_letters)
  memory_mb_per_node        = var.memory_gb_per_node * 1024
  discovery_ip              = split(":", google_memcache_instance.memcache_store.discovery_endpoint).0
  discovery_port            = split(":", google_memcache_instance.memcache_store.discovery_endpoint).1
  create_private_dns        = var.dns_zone_name == "" ? false : true
}

data "google_client_config" "google_client" {}

resource "google_project_service" "memcache_api" {
  service            = "memcache.googleapis.com"
  disable_on_destroy = false
}

resource "google_project_service" "dns_api" {
  service            = "dns.googleapis.com"
  disable_on_destroy = false
}

resource "google_memcache_instance" "memcache_store" {
  provider           = google-beta
  name               = local.memory_store_name
  display_name       = local.memory_store_display_name
  region             = local.region
  zones              = local.zones
  authorized_network = var.vpc_network_id
  memcache_version   = var.memcache_version
  node_count         = var.node_count
  node_config {
    cpu_count      = var.cpu_per_node
    memory_size_mb = local.memory_mb_per_node
  }
  depends_on = [google_project_service.memcache_api]
  timeouts {
    create = var.memcache_timeout
    update = var.memcache_timeout
    delete = var.memcache_timeout
  }
}

resource "google_dns_record_set" "memcache_subdomain" {
  count        = local.create_private_dns ? 1 : 0
  managed_zone = var.dns_zone_name
  name         = format("%s.%s", var.dns_subdomain, data.google_dns_managed_zone.dns_zone.dns_name)
  type         = "A"
  rrdatas      = [local.discovery_ip]
  ttl          = var.dns_ttl
}

data "google_dns_managed_zone" "dns_zone" {
  name         = var.dns_zone_name
  depends_on   = [google_project_service.dns_api]
}
